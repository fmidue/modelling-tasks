\documentclass{article}

\usepackage[margin=2cm]{geometry}

\usepackage[utf8]{inputenc}

\usepackage{tikz}
\usetikzlibrary{petri}

\usepackage{listings}
\usepackage{alloy-style}
\lstset{
  language=alloy,
  frame=single,
  breaklines=true,
  postbreak=\raisebox{0ex}[0ex][0ex]{\ensuremath{\color{red}\hookrightarrow\space}},
  basicstyle=\normalsize\ttfamily,
  % showstringspaces=false,
  % stringstyle=\ttfamily,
  % commentstyle=\color{white}
}

\pagestyle{empty}

\begin{document}

\section*{Concerning multiplicities}

Make all multiplicities explicit.
For example, write
\begin{lstlisting}
  abstract sig Place extends Node
  {
    tokens : one Int
  }
\end{lstlisting}
instead of
\begin{lstlisting}
  abstract sig Place extends Node
  {
    tokens : Int
  }
\end{lstlisting}
That applies also to relational bounding expressions and in quantifiers.
For example, write
\begin{lstlisting}
  abstract sig Node
  {
    flow : Node set -> lone Int
  }
  {
    all v : one flow[Node] | v > 0
  }
\end{lstlisting}
instead of
\begin{lstlisting}
  abstract sig Node
  {
    flow : Node -> lone Int
  }
  {
    all v : flow[Node] | v > 0
  }
\end{lstlisting}
Likewise, be explicit about what the intended multiplicity of arguments is in predicate definitions.
For example, write
\begin{lstlisting}
  pred concurrencyMultiple(ts : set Transition) {
    all t : one ts | enabled[t]
    all p : one Place | p.tokens >= (sum s : ts | p.flow[s])
  }
\end{lstlisting}
instead of
\begin{lstlisting}
  pred concurrencyMultiple(ts : Transition) {
    all t : one ts | enabled[t]
    all p : one Place | p.tokens >= (sum s : ts | p.flow[s])
  }
\end{lstlisting}
Analogously for function definitions.

\section*{Concerning ``type-checking'' of function/predicate use}

Even though Alloy does not actually check the multiplicities, in particular, scalar vs.\ actual sets, make sure that all uses are consistent with intended multiplicities of arguments in definitions.
For example, given the definition
\begin{lstlisting}
  pred enabled(t : one Transition) {
    all p : one Place | p.tokens >= p.flow[t]
  }
\end{lstlisting}
do \emph{not} do something like
\begin{lstlisting}
  pred concurrencyMultiple(ts : set Transition) {
    enabled[ts]
    all p : one Place | p.tokens >= (sum s : ts | p.flow[s])
  }
\end{lstlisting}

\section*{Concerning implicit summing of integer sets}

Do not rely, except in very clear circumstances, on the implicit summing of integer sets in comparison expressions.
That is, in a comparison like \lstinline|a>=b|, where \lstinline|a| or \lstinline|b| might be an actual set (with possibly more than one element), rather write explicitly \lstinline|a.sum>=b.sum|.
However,
\begin{lstlisting}
  pred enabled(t : one Transition) {
    all p : one Place | p.tokens >= p.flow[t]
  }
\end{lstlisting}
is okay, since by the definition of \lstinline|flow| we know that \lstinline|p.flow[t]| is empty or a singleton, so we can avoid writing more explicitly \lstinline|p.flow[t].sum|.

\section*{Concerning arithmetic comparisons}

Always be as strict as possible.
For example, write
\begin{lstlisting}
  pred outComing(t : one Transition) {
    (sum p : one Place | t.flow[p]) > 0
  }
\end{lstlisting}
instead of
\begin{lstlisting}
  pred outComing(t : one Transition) {
    (sum p : one Place | t.flow[p]) != 0
  }
\end{lstlisting}

\section*{Concerning precedences in expressions}

Add extra parentheses even when Alloy's precedence rule ``\lstinline|&| binds tighter than \lstinline|=|'' would make if possible to avoid them.
For example, write
\begin{lstlisting}
  fact {
    all p : one Place | (p.flow.Int & Place) = none
  }
\end{lstlisting}
instead of
\begin{lstlisting}
  fact {
    all p : one Place | p.flow.Int & Place = none
  }
\end{lstlisting}

\section*{Concerning logical operators}

Always use the verbose forms of logical operators, that is, \lstinline|not|, \lstinline|and|, \lstinline|or|, \lstinline|implies|, \lstinline|iff|, instead of their symbolic shorthand forms.

\end{document}

% ---------------------------------------------------------------------------- %
%% Local Variables:
%% coding: utf-8
%% ispell-local-dictionary: "british"
%% End:
